plugins {
    id 'org.springframework.boot' apply false
    id 'io.spring.dependency-management' apply false
}
ext {
    springCloudVersion = "2020.0.3"
    apacheCommonLang3Version = "3.11"
    apacheCommonPool2Version = "2.8.1"
    apacheCommonIOVersion = "2.7"
    mapstructVersion = "1.4.2.Final"
    lombokVersion = "1.18.16"
    lombokMapstructBindingVersion = "0.2.0"
}
subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    group = 'com.team'
    version = '0.1.0-SNAPSHOT'

    sourceCompatibility = "11"
    targetCompatibility = "11"

    tasks.withType(JavaCompile) {
        options.encoding('UTF-8')
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
        dependencies {
            dependency "org.apache.commons:commons-pool2:${apacheCommonPool2Version}"
            dependency "org.apache.commons:commons-lang3:${apacheCommonLang3Version}"
            dependency "commons-io:commons-io:${apacheCommonIOVersion}"
            dependency "org.mapstruct:mapstruct:${mapstructVersion}"
            dependency "org.mapstruct:mapstruct-processor:${mapstructVersion}"
            dependency "org.projectlombok:lombok:${lombokVersion}"
            dependency "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
        }
    }

    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-consul-config'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation 'io.github.openfeign:feign-jackson'

        implementation "org.apache.commons:commons-pool2"
        implementation "org.apache.commons:commons-lang3"
        implementation 'commons-io:commons-io'

        implementation "org.mapstruct:mapstruct", "org.projectlombok:lombok"
        annotationProcessor "org.mapstruct:mapstruct-processor", "org.projectlombok:lombok", "org.projectlombok:lombok-mapstruct-binding"
        testImplementation "org.mapstruct:mapstruct", "org.projectlombok:lombok"
        testAnnotationProcessor "org.mapstruct:mapstruct-processor", "org.projectlombok:lombok", "org.projectlombok:lombok-mapstruct-binding"

        testImplementation "org.springframework.boot:spring-boot-starter-test"
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}